// grammar definition for lelele.

@terminal LBRACKET, RBRACKET, AT_LBRACKET, COLON_EQ, EQ, COMMA, SEMICOLON, VERT_BAR;
@terminal TERMINAL, NONTERMINAL, START, RULE, EMPTY, PREC;
@terminal IDENT;

@nonterminal Grammar, Descs, Desc, TerminalDesc, NonterminalDesc, StartDesc, RuleDesc, PrecDesc;
@nonterminal Productions, Production, Configs, Config, Idents, Elems;
@start Grammar;

@rule Grammar := Descs ;

@rule Descs :=
    | @empty
    | Descs Desc SEMICOLON
    ;

@rule Desc :=
    | TerminalDesc
    | NonterminalDesc
    | RuleDesc
    | PrecDesc
    | StartDesc
    ;

@rule PrecDesc := PREC LBRACKET Configs RBRACKET IDENT ;

@rule TerminalDesc :=
    | TERMINAL Idents
    | TERMINAL LBRACKET Configs RBRACKET Idents
    ;

@rule NonterminalDesc := NONTERMINAL Idents ;

@rule StartDesc := START IDENT ;

@rule RuleDesc :=
    | RULE IDENT COLON_EQ Productions
    | RULE IDENT COLON_EQ VERT_BAR Productions
    ;

@rule Productions :=
    | Production
    | Productions VERT_BAR Production
    ;

@rule Production :=
    | EMPTY
    | Elems
    | AT_LBRACKET Configs RBRACKET Elems
    ;

@rule Elems :=
    | IDENT
    | IDENT Elems
    ;

@rule Idents :=
    | IDENT
    | IDENT COMMA
    | IDENT COMMA Idents
    ;

@rule Configs :=
    | Config
    | Config COMMA
    | Config COMMA Configs
    ;

@rule Config := IDENT EQ IDENT ;
